apply plugin: 'com.android.application'

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion

    defaultConfig {
        applicationId rootProject.ext.android.applicationId
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName
        //默认是友盟的渠道
        //manifestPlaceholders = [UMENG_CHANNEL_VALUE: "umeng"]
    }

    lintOptions {
        abortOnError rootProject.ext.android.abortOnError
    }

    //签名配置信息
    signingConfigs {

        def Properties localProps = new Properties()
        localProps.load(new FileInputStream(file('../local.properties')))
        def Properties keyProps = new Properties()

        // 如果读取不到'keystore.props.file'属性，就使用debug keystore

        if (localProps['keystore.props.file']) {
            keyProps.load(new FileInputStream(file(localProps['keystore.props.file'])))
        } else {
            keyProps["store"] = '../buildsystem/debug.keystore'
            keyProps["alias"] = 'android'
            keyProps["keyStorePassword"] = 'androiddebugkey'
            keyProps["keyPassword"] = 'android'
        }

        debug {
            /*  storeFile file(keyProps["store"])
              keyAlias keyProps["alias"]
              storePassword keyProps["keyStorePassword"]
              keyPassword keyProps["keyPassword"]*/
        }
         release {
             // release版本使用assert确保存在该属性否则报错，避免错误打包
             assert localProps['keystore.props.file'];
             storeFile file(keyProps["store"])
             keyAlias keyProps["alias"]
             storePassword keyProps["keyStorePassword"]
             keyPassword keyProps["keyPassword"]
         }
    }

    buildTypes {
        debug {
            //显示Log
            buildConfigField "boolean", "LOG_DEBUG", "true"
            versionNameSuffix "-debug"
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
            debuggable true
        }
        release {
            //不显示LOG
            buildConfigField "boolean", "LOG_DEBUG", "false"
            //混淆
            minifyEnabled false
            //移除无用的res
            shrinkResources false
            zipAlignEnabled true
           /* proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro'*/
            signingConfig signingConfigs.release
            //apk输出格式
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        // 输出apk名称为ganklock_v1.0_2015-01-15_wandoujia.apk
                        def fileName = "ganklock_v${defaultConfig.versionName}_${releaseTime()}_${variant.productFlavors[0].name}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
    }

    //友盟多渠道打包
    productFlavors {
        xiaomi {}
        wandoujia {}
        baidu {}
        fir {}
        qihu360 {}
    }

    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }
}

repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}




dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:25.0.0'
    compile 'com.android.support:recyclerview-v7:25.0.0'
    compile 'com.android.support:cardview-v7:25.0.0'
    compile 'com.android.support:design:25.0.0'
    compile 'com.google.code.gson:gson:2.7'
    compile 'io.reactivex:rxandroid:1.1.0'
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
    compile 'com.squareup.okhttp3:okhttp:3.4.1'
    compile 'com.squareup.okhttp3:logging-interceptor:3.4.1'
    compile 'com.jenzz:materialpreference:1.3'
    compile 'com.michaelpardo:activeandroid:3.1.0-SNAPSHOT'
    compile 'it.sephiroth.android.library.bottomnavigation:bottom-navigation:1.0.7'
    compile 'pl.droidsonroids.gif:android-gif-drawable:1.2.2'
    compile 'com.github.chrisbanes:PhotoView:1.3.0'
    compile 'com.bilibili:magicasakura:0.1.1@aar'
    compile 'com.artit-k:license-fragment:1.3.0'
    //debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5'
    //releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
}
